output "##########################################"
output "TESTING MATHEMATICAL PRECEDENCE"
print 17 + 3 * 8 / 6  // 21.0
print 38 / 9 * 27 + 3  // 117.0
print 174 mod 28 + 3 * 9  // 33

// Test floats AKA decimals
output "##########################################"
output "TESTING DECIMALS"
print 3.142 * 2  // 6.284
print 2.718 / 2  // 1.359
print 6.85 mod 0.37  // 0.18999999999999972

// Test exponentiation
output "##########################################"
output "TESTING EXPONENTS/POWERS"
print 2 ^ 31 - 1  // 2147483647
print 2 ^ 0.5  // 1.4142135623730951
print 3 ^ (-1/3)  // 0.6933612743506348

// Test parentheses
output "##########################################"
output "TESTING PARENTHESES"
print 3 + 1 / 2  // 3.5
print (3 + 1) / 2  // 2.0
print (-1 - 3) * 4  // -16
print -3 / 3  // -1.0

// Test unary operators
output "##########################################"
output "TESTING UNARY OPERATORS"
print 3 - - 7  // 10
print 5 - + + (3 + - - 10)  // -8

// Test variables
output "##########################################"
output "TESTING VARIABLES"
my_float = 1 + 3.0
my_int = 8
print my_float  // 4.0
print my_int  // 8
print my_float + my_int  // 12.0
print my_float - my_int  // -4.0

// Test strings AKA text
output "##########################################"
output "TESTING TEXT"
my_str1 = "hello"
my_str2 = "world"
print my_str1  // hello
print my_str2  // world
print my_str1 + ' ' + my_str2 + '!'  // hello world!

// Test booleans AKA conditions
output "##########################################"
output "TESTING CONDITIONS"
my_bool1 = tRuE
my_bool2 = FaLSe
print my_bool1  // true
print my_bool2  // false
my_bool3 = 1 = 1
my_bool4 = 1 = 2
print my_bool3  // true
print my_bool4  // false
print 'yes' not= 'no'  // true
print true = false  // false

// Test conditional operators
output "##########################################"
output "TESTING CONDITIONAL OPERATORS"
print 1 <= 2  // true
print 2.1 < 1  // false

// Test logical operators
output "##########################################"
output "TESTING LOGICAL OPERATORS"
print true and true  // true
print 1 = 2 or 3 = 2  // false
print not 1 = 2 and true  // true
print not(1 = 1 and not 2 = 3)  // false

// Test string concatenation
output "##########################################"
output "TESTING TEXT CONCATENATION"
print "I am " + 17 + " years old"  // I am 17 years old
print "Pi is approximately equal to " + 3.14159  // Pi is approximately equal to 3.14159
print "1 = 1 is " + (1 = 1)  // 1 = 1 is true
print 99 + " bottles of beer on the wall"  // 99 bottles of beer on the wall
print 2.71828 + " is the approximate value of e"  // 2.71828 is the approximate value of e

// Test string multiplication
output "##########################################"
output "TESTING TEXT MULTIPLICATION"
print 'W' + 'o' * 10 + 'w'  // Woooooooooow
print 'A' + 10 * 'a' + 'h'  // Aaaaaaaaaaah

// Test repeat loops
output "##########################################"
output "TESTING REPEAT LOOPS"

i = 1
repeat 2
	output "Looping " + i
	output i + " * 2 = " + i * 2
	i = i + 1

j = 1
k = 1
l = 1
repeat 2
	repeat 3
		repeat 4
			output "j=" + j + " k=" + k + " l=" + l
			j = j + 1
		output "j=" + j + " k=" + k + " l=" + l
		k = k + 1
	output "j=" + j + " k=" + k + " l=" + l
	l = l + 1

// Test repeat until loops
output "##########################################"
output "TESTING REPEAT UNTIL LOOPS"
m = 1
n = 1
o = 1
repeat until m > 2
	repeat until n > 3
		repeat until o > 4
			output "m=" + m + " n=" + n + " o=" + o
			o = o + 1
		output "m=" + m + " n=" + n + " o=" + o
		n = n + 1
	output "m=" + m + " n=" + n + " o=" + o
	m = m + 1

// Test if, else if, else conditional statements
output "##########################################"
output "TESTING CONDITIONAL STATEMENTS"
p = 0
if p > 0
	output "p is positive"
	print "p equals " + p
else if p < 0
	output "p is negative"
	print "p equals " + p
else
	output "p is zero"

if p >= 0
	if p = 0
		output "p is definitely zero"
	else
		output "p is definitely positive"
else
	output "p is definitely negative"

output "##########################################"
output "End of file!"